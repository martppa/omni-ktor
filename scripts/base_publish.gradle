task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.asere.omni.mvi'
            artifactId = ARTIFACT_ID
            version = VERSION
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = POM_NAME
                description = POM_DESCRIPTION
                url = 'https://github.com/martppa/omni-mvi'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'martppa'
                        name = 'Humberto Mart√≠n Dieppa'
                        email = 'hm.dieppa@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/martppa/omni-mvi.git'
                    developerConnection = 'scm:git:ssh://github.com/martppa/omni-mvi.git'
                    url = 'https://github.com/martppa/omni-mvi'
                }
            }
        }
    }
    repositories {
        maven {
            Properties properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = properties.getProperty("ossrhUsername")
                password = properties.getProperty("ossrhPassword")
            }
        }
    }
}
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}